apply plugin: 'com.android.application'

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
}
android {
    defaultConfig {
        applicationId 'net.anei.cadpage'
        versionName project.versionName
        versionCode project.versionCode
        targetSdkVersion 29
        minSdkVersion 14
    }
    dexOptions {
        jumboMode = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }

        flavorDimensions "store", "type"
        productFlavors {

            google {
                dimension "store"
                versionNameSuffix "G"
                versionCode 0
                buildConfigField "boolean", "APTOIDE", "false"
            }

            aptoide {
                dimension "store"
                versionNameSuffix "A"
                versionCode 1
                buildConfigField "boolean", "APTOIDE", "true"
            }

            full {
                dimension "type"
                versionNameSuffix "M"
                versionCode 1
                buildConfigField "boolean", "MSG_ALLOWED", "true"
            }

            restrict {
                dimension "type"
                versionNameSuffix "R"
                versionCode 0
                buildConfigField "boolean", "MSG_ALLOWED", "false"
            }
        }
    }

    applicationVariants.all { variant ->
        def storeVersion = variant.productFlavors.get(0).versionCode
        def typeVersion = variant.productFlavors.get(1).versionCode
        def version = storeVersion * 1000000000 + defaultConfig.versionCode + typeVersion
        variant.outputs.each { output ->
            output.versionCodeOverride = version
        }
    }

    compileSdkVersion 29

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '28.0.3'
}

dependencies {
    implementation project(':cadpage-parsers')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-iid:17.1.0'
    implementation 'com.google.firebase:firebase-messaging:17.4.0'
    implementation 'android.arch.work:work-runtime:1.0.0'
    implementation 'android.arch.lifecycle:common-java8:1.0.0'
    implementation 'com.android.billingclient:billing:2.0.3'
}

apply plugin: 'com.google.gms.google-services'
