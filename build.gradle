apply plugin: 'com.android.application'

allprojects {
    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
        maven { url "https://dl.bintray.com/blockchainds/bds" }
    }
}
android {
    defaultConfig {
        applicationId 'net.anei.cadpage'
        versionName project.versionName
        versionCode project.versionCode
        targetSdkVersion 30
        minSdkVersion 16
        multiDexEnabled true
    }
    dexOptions {
        javaMaxHeapSize "4G"
        jumboMode = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }

        flavorDimensions "store", "type"
        productFlavors {

            google {
                dimension "store"
                versionNameSuffix "G"
                versionCode 1
                buildConfigField "boolean", "APTOIDE", "false"
            }

            aptoide {
                dimension "store"
                versionNameSuffix "A"
                versionCode 1
                buildConfigField "boolean", "APTOIDE", "true"
            }

            full {
                dimension "type"
                versionNameSuffix "M"
                versionCode 2
                buildConfigField "boolean", "MSG_ALLOWED", "true"
            }

            restrict {
                dimension "type"
                versionNameSuffix "R"
                versionCode 0
                buildConfigField "boolean", "MSG_ALLOWED", "false"
            }
        }
    }

    applicationVariants.all { variant ->
        def storeVersion = variant.productFlavors.get(0).versionCode
        def typeVersion = variant.productFlavors.get(1).versionCode
        def version = defaultConfig.versionCode + typeVersion * 10000000
        variant.outputs.each { output ->
            output.versionCodeOverride = version
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("aptoide") && !names.contains("full")) {
            setIgnore(true)
        }
    }

    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
//    buildToolsVersion '28.0.3'
}

dependencies {
    implementation project(':cadpage-parsers')
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.firebase:firebase-core:18.0.0'
    implementation 'com.google.firebase:firebase-iid:21.0.1'
    implementation 'com.google.firebase:firebase-messaging:21.0.1'
    implementation 'androidx.work:work-runtime:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation ('com.klinkerapps:android-smsmms:5.2.5') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
        exclude group: 'com.squareup.okhttp', module: 'okhttp-urlconnection'
    }
    googleImplementation 'com.android.billingclient:billing:3.0.2'
    aptoideImplementation 'com.blockchainds:appcoins-ads:0.5.1.32b'
    aptoideImplementation 'com.blockchainds:android-appcoins-billing:0.5.1.32b'
    aptoideImplementation 'com.blockchainds:appcoins-contract-proxy:0.5.1.32b'
}

apply plugin: 'com.google.gms.google-services'
